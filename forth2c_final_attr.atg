COMPILER Forth2c

String code = "";
int i = 0;
String main = "void main(){\n stack_work = init_stack(); \n stack_result = init_stack(); \n";
String tmp = code;
String var1 = "";
String var2 = "";
String var = "i";

public String cabecalho(String str){

	str = add(str, "#include <stdio.h>");
	str = add(str, "#include <math.h>");
	str = add(str, "#include \"lforth.h\"");
	str = add(str, "struct type_stack *stack_work;");
	str = add(str, "struct type_stack *stack_result;");		
	str = add(str, "float i;");		
	return str;
}

public String variavel(String str){

	return str + (Integer.toString(i));
}


public String make_function(String str, String name){

	str = add(str, "void " + name + "() {");
	return str;
}

public String call_function(String str, String name, String parametros){

	str = add(str, name + "(" + parametros + ");");
	return str;
}

public String call_function(String str, String name){

	str = add(str, name + "();");
	return str;
}

public String add(String str, String cmd){
	str = str + cmd +"\n";
	return str;
}

public String unary_operator(String str, String token){

	str = add(str, "push(stack_work, " + token + ");");
	return str;
}

public String between_operator(String str, String token){

	str = add(str, "i = drop(stack_work); push (stack_work, drop(stack_work) " + token + " i );");
	return str;
}


CHARACTERS 
        letramin = "abcdefghijklmnopqrstuvwxyz".
        letramai = "ABCDEFGHIJKLMNOPQRSTUVWXYZ".
        caracter = ANY - "\"".
        digito = "1234567890".

TOKENS
        identifFuncao = letramin {letramin | letramai | digito}.
        numero = ["-"] digito {digito} ["." digito {digito}].
        string = "\""  caracter {caracter} "\"".

COMMENTS FROM '(' TO ')' NESTED

IGNORE '\n' + '\t' + '\r'

PRODUCTIONS

        Forth2c = 										(.tmp =  cabecalho(tmp); .)
		bloco 											
		{												
			bloco
		}												(.main =  add(main, "}"); System.out.print(code + main); .)
		. 												

        bloco = 	deffuncao							
					|   								(. tmp = ""; .)
						sequencia						
														(. main += tmp; .)
			.								


        instrucaoIO =  "." 								(.String tmp1 = "\"%f\", drop(stack_work)"; .)
					[
					  string							(. tmp1 = t.val; .)
					]									(. tmp = add(tmp, "printf(" + tmp1 + ");"); .)
					.
		//tratar o zero...
        opAritmetico_dois = "+" | "-" | "*" | "/".
		opAritmetico_um = "SQRT"						(. tmp = add(tmp, "push (stack_work, sqrt(drop(stack_work))) ;"); .)
							.
		opAritmetico_diff = "%" 						(. tmp = add(tmp, "push (stack_work, ((int)drop(stack_work)) % ((int)(drop(stack_work))));"); .)
							.
        opLogico_dois = ">" | "<". 
		opLogico_diff = "OR" 							(. tmp = between_operator(tmp, "||" ); .)
						|"AND"							(. tmp = between_operator(tmp, "&&" ); .)
						| "="						    (. tmp = between_operator(tmp, "==" ); .)
						.
        opLogico_um = "NOT" 							(. tmp =  add(tmp, "push(stack_work, !drop(stack_work));"); .)
					| "EMPTY" 							(. tmp =  add(tmp, "empty(stack_work);"); .)
					| "REMPTY"							(. tmp =  add(tmp, "rempty(stack_result, stack_work);"); .)
					.
        funcaoForth = "DROP"							(. String p = "stack_work"; tmp = call_function(tmp, t.val.toLowerCase(), p); .)  
				| "SWAP"  								(. String p = "stack_work"; tmp = call_function(tmp, t.val.toLowerCase(), p); .)
				| "DUP"					   				(. String p = "stack_work"; tmp = call_function(tmp, t.val.toLowerCase(), p); .)
				| "ROT"   								(. String p = "stack_work"; tmp = call_function(tmp, t.val.toLowerCase(), p); .)
				| "PICK"								(. String p = "stack_work, drop(stack_work)" ; tmp = call_function(tmp, "pick", p); .) 								 
				| "ROLL" 								(. String p = "stack_work, drop(stack_work)" ; tmp = call_function(tmp, "roll", p); .) 
				| "SRAND" 								(. tmp = call_function(tmp, "srandf"); .)
				| "RAND" 								(. String p = "stack_work" ; tmp = call_function(tmp, "randf", p);  .) 
				| "CR"									(. tmp = call_function(tmp, t.val.toLowerCase()); .)	
				.

        deffuncao = ":"															
				     identifFuncao 						(. tmp = make_function(tmp, t.val); .)
														
					sequencia {sequencia} 
					";"									(. tmp = add(tmp, "}"); .)

														(. code += tmp; tmp = ""; .)
					.

		funcaoForth_nomes_obscuros =  ">R"    			(. tmp = add(tmp, "mv_work_to_result(stack_work, stack_result);"); .)
				| "R>"    					  			(. tmp = add(tmp, "mv_result_to_work(stack_result, stack_work);"); .)
				| "R@"						  			(. tmp = add(tmp, "r_arroba(stack_work, stack_result)"); .)
				| "?"						  			(. tmp = add(tmp, "scanf(\"%f\",&" + var + "); push(stack_work, " + var +");"); .)
				.

        
        instrucaoCond = "IF"							(. tmp = add(tmp, "if(drop(stack_work)) {") ; .)
		 sequencia {sequencia} [restoCond] 
		"THEN"											(. tmp = add(tmp, "}"); .)
		.
        restoCond =  "ELSE" 							(. tmp = add(tmp, "} else {"); .)
			sequencia {sequencia}.  
        
        instrucaoRep = repBeginUntil | repDoLoop.

		repBeginUntil =	"BEGIN" 							(. tmp = add(tmp, "do {"); .)
				sequencia {sequencia} 						(. tmp = add(tmp, "} while(!drop(stack_work));"); .)
				"UNTIL"
				.
        repDoLoop =      
				"DO" 									(. i++; var1 = variavel("i"); var2 = variavel("j"); .)
														(. tmp = add(tmp, "int " + var1 + ";" + "float "+ var2 + "=drop(stack_work); for("+ var1 +"=0;"+ var1 + " < " + var2 +";" + var1 +"++) {" );.)
				sequencia {sequencia} 
				"LOOP"									(. tmp =  add(tmp, "}"); .)
				.   

        sequencia =	
				instrucaoRep 	
				| funcaoForth																		
				| funcaoForth_nomes_obscuros					
				| numero 										(. tmp = add(tmp, "push(stack_work, " + t.val + ");"); .)
				| opAritmetico_dois 							(. tmp = between_operator(tmp, t.val); .)
				| opAritmetico_um								
				| opAritmetico_diff
				| opLogico_diff
				| opLogico_dois  								(. tmp = between_operator(tmp, t.val); .)
				| opLogico_um  								    
				| identifFuncao 								(. tmp = call_function(tmp, t.val); .)
				| instrucaoIO  
				| instrucaoCond 
				.
END Forth2c.
        
